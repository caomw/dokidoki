/*
 * dokiScript.grammar
 */

%header%
GRAMMARTYPE   = "LL"
CASESENSITIVE = "true"
AUTHOR        = "Kesumu,Aetf"
VERSION     = "1.0"
DATE        = "2016-03-17"


/** Token definitions **/
%tokens%

WORLD = "world"

BACKGROUND = "background"
WEATHER = "weather"
SOUND = "sound"
BGM = "bgm"
VIDEO = "video"
MOVE = "move"
POSTURE = "posture"
VOICE = "voice"
ROLE = "role"


SRC = "src"
TRANSITION = "transition"
TRANSITION_INSTANT = "instant"
TRANSITION_GRADUAL = "gradual"
SPEED = "speed"
TYPE = "type"
TYPE_SUNNY = "sunny"
TYPE_RAIN = "rain"
TYPE_SNOW = "snow"
LEVEL = "level"
MODE = "mode"
MODE_LOOP = "loop"
POSITION = "position"
POSITION_CENTER = "center"
POSITION_LEFT = "left"
POSITION_RIGHT = "right"
TYPE_PLAYER = "player"
TYPE_CHARACTER = "character"
NAME = "name"
ANCHOR = "anchor"


BRACKET_LEFT = "{"
BRACKET_RIGHT = "}"
SQUARE_BRACKET_LEFT = "["
SQUARE_BRACKET_RIGHT = "]"
PARENTHESE_LEFT = "("
PARENTHESE_RIGHT = ")"
ANGLE_BRACKET_LEFT = "<"
DOUBLE_QUOTE = '"'
PERIOD = "."
COMMA = ","
SEMICOLON = ";"
EQUAL = "="
CLICK = ">"
CLICK_NEXT_DIALOGUE_PAGE = ">>"
OR = "|"

TAB = <<\t+>>
RETURN = <<[\n\r]+>>
SPACE = << +>>
IDENTIFIER = <<[a-zA-Z_][0-9a-zA-Z_]*>>
DECIMAL = <<[01]\.?\d*>>
TEXT = <<>.*>>
QUOTED_TEXT = <<"[^"]*">>


/** Production definitions **/
%productions%

Doki  	= (Part)+;
Part 	= Block
		| Flag
		| Option
		| Jump ;
Block 	= WORLD ([RETURN] Action)+ [SPACE] RETURN
		| IDENTIFIER ([RETURN] Action)+ [SPACE] RETURN;
Action 	= TAB VoiceAction
		| TAB OtherAction
		| TAB TEXT;
VoiceAction = VOICE SPACE {Key [SPACE] EQUAL [SPACE] Value [SPACE]} TEXT [SPACE] ;
OtherAction = Tag SPACE {Key [SPACE] EQUAL [SPACE] Value [SPACE]} SEMICOLON [SPACE] ;
Tag		= BACKGROUND
		| WEATHER
		| SOUND
		| BGM
		| VIDEO
		| MOVE
		| POSTURE
		| ROLE ;
Key		= SRC
		| TRANSITION
		| TYPE
		| LEVEL
		| MODE
		| POSITION
		| NAME 
		| ANCHOR ;
Value 	= PARENTHESE_LEFT [SPACE] DECIMAL [SPACE] COMMA [SPACE] DECIMAL [SPACE] COMMA [SPACE] DECIMAL [SPACE] PARENTHESE_RIGHT
		| PARENTHESE_LEFT [SPACE] DECIMAL [SPACE] COMMA [SPACE] DECIMAL [SPACE] PARENTHESE_RIGHT
		| TRANSITION_INSTANT
		| TRANSITION_GRADUAL
		| TYPE_SUNNY
		| TYPE_RAIN
		| TYPE_SNOW
		| MODE_LOOP
		| POSITION_CENTER
		| POSITION_LEFT
		| POSITION_RIGHT
		| TYPE_PLAYER
		| TYPE_CHARACTER
		| IDENTIFIER
		| DECIMAL
		| QUOTED_TEXT;
Flag 	= SQUARE_BRACKET_LEFT [SPACE] QUOTED_TEXT [SPACE] PARENTHESE_LEFT [SPACE] IDENTIFIER [SPACE] COMMA [SPACE] IDENTIFIER [SPACE] PARENTHESE_RIGHT {[SPACE] OR [SPACE] QUOTED_TEXT [SPACE] PARENTHESE_LEFT [SPACE] IDENTIFIER [SPACE] COMMA [SPACE] IDENTIFIER [SPACE] PARENTHESE_RIGHT} [SPACE] SQUARE_BRACKET_RIGHT [SPACE] RETURN;
Option	= ANGLE_BRACKET_LEFT [SPACE] IDENTIFIER [SPACE] CLICK [SPACE] RETURN;
Jump 	= PARENTHESE_LEFT [SPACE] IDENTIFIER [SPACE] PARENTHESE_RIGHT [SPACE] RETURN;
